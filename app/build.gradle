apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    def versionPropsFile = file('build.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['BUILD_NUMBER'].toInteger() + 1
        def majorVersion = versionProps['MAJORver'].toInteger()
        def minorVersion = versionProps['MINORver'].toInteger()
        def bugfixVersion = versionProps['BUGFIXver'].toInteger()
        def author = versionProps['AUTHOR']

        versionProps['BUILD_NUMBER']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "diet.diet"
            minSdkVersion 19
            targetSdkVersion 19
            versionCode code
            versionName majorVersion.toString() + "." + minorVersion.toString() + "." + bugfixVersion.toString() + "." + code.toString()
            buildConfigField ("String", "AUTHOR", author)
            buildConfigField ("int", "MAJORver", majorVersion + "")
            buildConfigField ("int", "MINORver", minorVersion + "")
            buildConfigField ("int", "BUGFIXver", bugfixVersion + "")
            buildConfigField ("long", "TIMESTAMP", System.currentTimeMillis() + "L")
        }
    }
    else {
        throw new GradleException("Could not read build.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent,
                            output.outputFile.name.replace("app-release", "Diet"))
                }
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.1.0'
//    compile files('libs/ksoap2-android-assembly-2.5.8-jar-with-dependencies.jar')
//    compile files('libs/ksoap2-android-assembly-2.6.0-jar-with-dependencies.jar')
//    compile files('libs/ksoap2-android-assembly-3.3.0-jar-with-dependencies.jar')
//    compile files('libs/hellocharts-library-1.5.8.jar')
    compile 'com.google.android.gms:play-services-appindexing:8.4.0'
}
